* plain text stays as it is, with line wrapping
* [name](url) is a link
* ``` is multi-line code (ie. don't wrap lines)
* `t<id> for a textarea
* `c<id> for a div
* `sc< and >`sc for javascript
* todo: fix newline whitespace on jslisp, ski.wm

     DEFINE("unlink(s)", "ulk") :(unst)
ulk  s "<a href='" arb . link "'>" arb . name "</a>" = "[" name "](" link ")" :s(ulk)
     unlink = s                                                               :(return)

unst DEFINE("unesc(s)", "ugt") :(st)
ugt  s "&lt;" = "<"            :s(ugt)
ult  s "&gt;" = ">"            :s(ult)
     unesc = unlink(s)         :(return)


st   output = "<!DOCTYPE html>"
     output = "<head>"
     output = "  <title>Garklein's stuff</title>"
     output = "  <style>"
     output = ":root          { display:flex; justify-content:center; }"
     output = "pre            { margin:auto; }"
     output = "pre:not(.code) { white-space:pre-wrap; max-width:80ch; }"
     output = "  </style>"
     output = "</head>"
     output = "<body>"
     output = "  <pre>"

     nl = "*~3|a."
     file = input

in   file = file nl input :s(in)
     file = file nl

gt   file "<" = "&lt;" :s(gt)
lt   file ">" = "&gt;" :s(lt)


inp  file "`t&lt;" arb . id "&gt;" = "<textarea id='" id "'></textarea>" :s(inp)
div  file "`d&lt;" arb . id "&gt;" = "<div id='"      id "'></div>"      :s(div)

link file "[" break("[]") . name "](" break(")") . link ")" = "<a href='" link "'>" name "</a>" :s(link)
code file "```"     nl arb . c "```"     = "</pre><pre class='code'>" unlink(c) "</pre><pre>"   :s(code)
scpt file "`sc&lt;" nl arb . c "&gt;`sc" = "<script>"                 unesc(c) "</script>"      :s(scpt)

out  file arb . output nl =  :s(out)

     output = "<br><pre style='max-width:4ch'><a href='./index.html' >home</a></pre>"
     output = "  </pre>"
     output = "</body>"
end
