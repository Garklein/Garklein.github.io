* plain text stays as it is
* <name>`l<url> is a link
* `c<x>`c for `x` as inline code
* ``` is multi-line code
* `t<id> for a textarea
* `c<id> for a div
* `sc< and >`sc for javascript

     DEFINE("unesc(s)", "ugt") :(st)
ugt  s "&lt;" = "<"            :s(ugt)
ult  s "&gt;" = ">"            :s(ult)
     unesc = s                 :(return)

st   output = "<!DOCTYPE html>"
     output = "<head>"
     output = "  <title>Garklein's stuff</title>"
     output = "  <style>"
     output = "pre {"
     output = "  white-space: pre-wrap;"
     output = "}"
     output = "@font-face {"
     output = "  font-family: 'Courier';"
	 output = "  src: url('CourierPrime-Regular.ttf');"
	 output = "}"
     output = ".code {"
     output = "  font-family: 'Courier';"
     output = "}"
     output = "  </style>"
     output = "</head>"
     output = "<body>"
     output = "  <pre>"

     nl = "*~3|a."
     file = input

in   file = file nl input :s(in)
     file = file nl

gt   file "<" = "&lt;" :s(gt)
lt   file ">" = "&gt;" :s(lt)

link file "&lt;" arb . name "&gt;`l&lt;" arb . link "&gt;" = "<a href='" link "'>" name "</a>" :s(link)

inp  file "`t&lt;" arb . id "&gt;" = "<textarea id='" id "'></textarea>" :s(inp)
div  file "`d&lt;" arb . id "&gt;" = "<div id='"      id "'></div>"      :s(div)

cd1  file "`c&lt;" = "<span class='code'>" :s(cd1)
cd2  file "&gt;`c" = "</span>" :s(cd2)

code file "```"     nl arb . c "```"     nl = "<div class='code'>" c "</div>"     :s(code)
scpt file "`sc&lt;" nl arb . c "&gt;`sc" nl = "<script>"     unesc(c) "</script>" :s(scpt)

out  file arb . output nl =  :s(out)

     output = "  </pre>"
     output = "</body>"
end
