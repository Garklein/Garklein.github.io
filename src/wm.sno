* plain text stays as it is, with line wrapping
* [name](url) is a link
* ``` is multi-line code (ie. don't wrap lines)
* `t<id> for a textarea
* `c<id> for a div
* `sc< and >`sc for javascript

     DEFINE("unlink(s)", "ulk") :(unst)
ulk  s "<a href='" arb . link "'>" arb . name "</a>" = "[" name "](" link ")" :s(ulk)
     unlink = s                                                               :(return)

unst DEFINE("unesc(s)", "ugt") :(st)
ugt  s "&lt;" = "<"            :s(ugt)
ult  s "&gt;" = ">"            :s(ult)
     unesc = unlink(s)         :(return)

st   output = "<!DOCTYPE html>"
     output = "<html>"
     output = "<head>"
     output = "  <title>Garklein's stuff</title>"
     output = "  <style>"
     output = "body           { display:flex; flex-direction:column; justify-content:center; min-height:100vh;"
     output = "                 align-items:center; margin:0; font-size:1rem; }"
     output = "pre            { margin:auto; }"
     output = "pre:not(.code) { white-space:pre-wrap; max-width:80ch; }"
     output = "  </style>"
     output = "</head>"
     output = "<body><div><br>"

     filename = input

     nl = "*~3|a."
     file = input

in   file = file nl input :s(in)
     file = file nl

gt   file "<" = "&lt;" :s(gt)
lt   file ">" = "&gt;" :s(lt)


inp  file "`t&lt;" arb . id "&gt;" = "<textarea id='" id "'></textarea>" :s(inp)
div  file "`d&lt;" arb . id "&gt;" = "<div id='"      id "'></div>"      :s(div)

link file "[" break("[]") . name "](" break(")") . link ")" = "<a href='" link "'>" name "</a>" :s(link)
code file "```"     nl arb . c "```"     = "</pre><pre class='code'>" nl unlink(c) "</pre><pre>"   :s(code)
scpt file "`sc&lt;" nl arb . c "&gt;`sc" = "<script>"                 nl unesc(c) "</script>"      :s(scpt)

     file = "<pre>" nl file
     file = file "</pre>"
     file pos(0) "<pre>" nl "</pre>"         = ""
     file        "<pre>" nl "</pre>" rpos(0) = ""
     file = file nl
out  file arb . output nl =  :s(out)

*    todo: centre this stuff

     filename break("/") . folder "/" break(".") . file ".wm"
     home = "./index.html"
der  filename "/" = "" :f(hm)
     home = "../" home :(der)
hm   links = "<pre style='width:100%;text-align:center'><br><a href='" home "'>~</a>"
     links = links " <a href='./" folder ".html'>" folder "</a>"
     output = links "</pre>"
     output = "<br><br></div></body>"

     output = "</html>"
end
